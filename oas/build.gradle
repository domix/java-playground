plugins {
  id 'buildlogic.java-library-conventions'
  id "org.openapi.generator" version "7.16.0"
}

openApiGenerate {
  generatorName = "java"
  inputSpec = "$projectDir/src/main/resources/bando-openapi.json"
  outputDir = "${ buildDir }/generated"
  apiPackage = "com.circulosiete.sp.client.api"
  modelPackage = "com.circulosiete.sp.client.model"
  invokerPackage = "com.circulosiete.sp.invoker"
  skipValidateSpec = true
  additionalProperties = [
    library                : "native",
    dateLibrary            : "java8",          // java.time.*
    //useJakartaEe           : "true",          // Spring Boot 3+
    //useBeanValidation      : "true",
    hideGenerationTimestamp: "true",
    serializationLibrary   : "jackson"
  ]
}

sourceSets {
  main.java.srcDir("${ buildDir }/generated/src/main/java")
}

tasks.named('compileJava') {
  dependsOn(tasks.named('openApiGenerate'))
}

tasks.named('openApiGenerate') {
  outputs.dir("${ buildDir }/generated/src/main/java")
}

tasks.named('clean') {
  delete("${ buildDir }/generated")
}

ext {
  jakarta_annotation_version = "1.3.5"
}

dependencies {
  implementation(platform(libs.jackson.bom))

  implementation "com.google.code.findbugs:jsr305:3.0.2"
  implementation "com.fasterxml.jackson.core:jackson-core"
  implementation "com.fasterxml.jackson.core:jackson-annotations"
  implementation "com.fasterxml.jackson.core:jackson-databind"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
  implementation "org.openapitools:jackson-databind-nullable:0.2.7"
  implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
//  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
}
